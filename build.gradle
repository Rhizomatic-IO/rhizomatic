buildscript {

    repositories {
        jcenter()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.javamodularity:moduleplugin:1.5.0"
    }

}

plugins {
    id 'com.gradle.plugin-publish' version '0.9.9'
    id "com.jfrog.bintray" version "1.8.4"
}

project.description 'Rhizomatic'

def junitVersion = '5.0.0'

subprojects {
    group 'io.rhizomatic'
    version '0.3.2'

    repositories {
        jcenter()
        mavenLocal()
    }

    apply plugin: 'java'
    apply plugin: "org.javamodularity.moduleplugin"
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    tasks.withType(Javadoc).all { enabled = false } // disable javadoc due to Gradle Java9 issue

    dependencies {
        compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
        testCompile 'org.junit.jupiter:junit-jupiter-api:' + junitVersion
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:' + junitVersion
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from "../notice/javadoc/README.md"
    }


    def pomConfig = {
        licenses {
            license {
                name 'MIT'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'rhizomatic-io'
                name 'Rhizomatic'
                email "dev@null.com"
            }
        }

        scm {
            url 'https://github.com/Rhizomatic-IO/rhizomatic'
        }
    }


    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'Rhizomatic modular runtime')
                    root.appendNode('name', 'Rhizomatic')
                    root.appendNode('url', 'https://github.com/Rhizomatic-IO/rhizomatic')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    // secrets stored in ~/.gradle/gradle.properties NB key is the Bintray API key, not password
    def bUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : 'undefined'
    def bKey = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : 'undefined'

    bintray {
        user = bUser
        key = bKey
        publications = ['mavenJava']
        pkg {
            repo = 'Rhizomatic'
            name = project.name
            userOrg = 'rhizomatic'
            licenses = ['MIT']
            websiteUrl = 'https://github.com/Rhizomatic-IO/rhizomatic'
            issueTrackerUrl = 'https://github.com/Rhizomatic-IO/rhizomatic/issues'
            vcsUrl = 'https://github.com/Rhizomatic-IO/rhizomatic.git'
            version {
                name = project.version
                desc = 'Rhizomatic'
                released = new Date()
                vcsTag = project.version
            }
        }
    }



}






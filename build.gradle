buildscript {

    repositories {
        mavenCentral()
        gradlePluginPortal()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.javamodularity:moduleplugin:1.8.10"
    }

}

plugins {
    id 'com.gradle.plugin-publish' version '0.9.9'
}

project.description 'Rhizomatic'

def junitVersion = '5.8.2'

subprojects {
    group 'io.rhizomatic'
    version '0.3.5'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    apply plugin: 'java'
    apply plugin: "org.javamodularity.moduleplugin"
    apply plugin: 'maven-publish'

    tasks.withType(Javadoc).all { enabled = false } // disable javadoc due to Gradle Java9 issue

    dependencies {
        implementation group: 'org.jetbrains', name: 'annotations', version: '15.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-bom:' + junitVersion
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
    }

//    task javadocJar(type: Jar) {
//         from "../notice/javadoc/README.md"
//    }


    def pomConfig = {
        licenses {
            license {
                name 'MIT'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'rhizomatic-io'
                name 'Rhizomatic'
                email "dev@null.com"
            }
        }

        scm {
            url 'https://github.com/Rhizomatic-IO/rhizomatic'
        }
    }


    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'Rhizomatic modular runtime')
                    root.appendNode('name', 'Rhizomatic')
                    root.appendNode('url', 'https://github.com/Rhizomatic-IO/rhizomatic')
                    root.children().last() + pomConfig
                }
            }
        }
    }


}





